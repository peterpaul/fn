group 'net.kleinhaneveld.fn'
version '0.1.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = 'fn'
sourceCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}

dependencies {
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
}

uploadArchives {
    repositories {
        mavenLocal()
    }
}

if (project.hasProperty('fn.deployment.enabled')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Functional Programming Utilities for Java 7'
                    description 'Functional Programming Utilities for Java 7'
                    packaging 'jar'
                    url 'https://github.com/peterpaul/fn'

                    scm {
                        connection 'scm:git:https://github.com/peterpaul/fn.git'
                        developerConnection 'scm:git:https://github.com/peterpaul/fn.git'
                        url 'https://github.com/peterpaul/fn'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id 'peterpaul'
                            name 'Peterpaul Taekele Klein Haneveld'
                            email 'pp.kleinhaneveld@gmail.com'
                        }
                    }
                }
            }
        }

        signing {
            required {
                gradle.taskGraph.hasTask("uploadArchives")
            }
            sign configurations.archives
        }

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }
    }
}
